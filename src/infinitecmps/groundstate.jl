using Printf

# groundstate with UniformCMPS
function groundstate(Ĥ::LocalHamiltonian, Ψ₀::UniformCMPS; kwargs...)
    return groundstate_unconstrained(Ĥ, Ψ₀; kwargs...)
end

function groundstate(Ĥ::LocalHamiltonian, Ψ₀::UniformCMPS, n₀::Number; kwargs...)
    return groundstate_constrained(Ĥ, Ψ₀, ntuple(k -> n₀, Val(length(Ψ₀.Rs))); kwargs...)
end

function groundstate(Ĥ::LocalHamiltonian,
                     Ψ₀::UniformCMPS{<:AbstractMatrix,N},
                     n₀s::NTuple{N,<:Number}; kwargs...) where {N}
    return groundstate_constrained(Ĥ, Ψ₀, n₀s; kwargs...)
end

function groundstate_unconstrained(Ĥ::LocalHamiltonian, Ψ₀::UniformCMPS;
                                   gradtol=1e-7,
                                   verbosity=2,
                                   optalg=LBFGS(; gradtol=gradtol, verbosity=verbosity - 2),
                                   eigalg=defaulteigalg(Ψ₀),
                                   linalg=defaultlinalg(Ψ₀),
                                   (finalize!)=OptimKit._finalize!,
                                   kwargs...)
    δ = 1
    function retract(x, d, α)
        ΨL, = x
        QL = ΨL.Q
        RLs = ΨL.Rs
        KL = copy(QL)
        for R in RLs
            mul!(KL, R', R, +1 / 2, 1)
        end

        dRs = d
        RdR = zero(QL)
        for (R, dR) in zip(RLs, dRs)
            mul!(RdR, R', dR, true, true)
        end

        RLs = RLs .+ α .* dRs
        KL = KL - (α / 2) * (RdR - RdR')
        QL = KL
        for R in RLs
            mul!(QL, R', R, -1 / 2, 1)
        end

        ΨL = InfiniteCMPS(QL, RLs; gauge=:l)
        ρR, λ, info_ρR = rightenv(ΨL, ρR; eigalg=eigalg, linalg=linalg, kwargs...)
        rmul!(ρR, 1 / tr(ρR[]))
        HL, E, e, hL, info_HL = leftenv(Ĥ, (ΨL, ρL, ρR); eigalg=eigalg, linalg=linalg,
                                        kwargs...)

        if info_ρR.converged == 0 || info_HL.converged == 0
            @warn "step $α : not converged, e = $E"
            @show info_ρR
            @show info_HL
        end

        return (ΨL, ρR, HL, E, e, hL), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        return 2 * real(sum(dot.(d1, d2)))
    end

    function precondition(x, d)
        ΨL, ρR, = x
        dRs = d
        return dRs .* Ref(posreginv(ρR[0], δ))
    end

    function fg(x)
        (ΨL, ρR, HL, E, e, hL) = x

        gradQ, gradRs = gradient(Ĥ, (ΨL, ρL, ρR), HL, zero(HL); kwargs...)

        Rs = ΨL.Rs

        dRs = .-(Rs) .* Ref(gradQ) .+ gradRs

        return E, dRs
    end

    scale!(d, α) = rmul!.(d, α)
    add!(d1, d2, α) = axpy!.(α, d2, d1)

    function _finalize!(x, E, d, numiter)
        normgrad2 = inner(x, d, d)
        δ = max(1e-12, 1e-3 * normgrad2)
        normgrad = sqrt(normgrad2)
        verbosity > 1 &&
            @info @sprintf("UniformCMPS ground state: iter %4d: e = %.12f, ‖∇e‖ = %.4e",
                           numiter, E, normgrad)
        return finalize!(x, E, d, numiter)
    end

    ΨL, = leftgauge(Ψ₀; kwargs...)
    ρR, λ, info_ρR = rightenv(ΨL; kwargs...)
    ρL = one(ρR)
    rmul!(ρR, 1 / tr(ρR[]))
    HL, E, e, hL, info_HL = leftenv(Ĥ, (ΨL, ρL, ρR); kwargs...)
    x = (ΨL, ρR, HL, E, e, hL)

    if info_ρR.converged == 0 || info_HL.converged == 0
        @warn "initial point not converged, energy = $E"
        @show info_ρR
        @show info_HL
    end

    verbosity > 0 &&
        @info @sprintf("UniformCMPS ground state: initialization with e = %.12f", E)

    x, E, grad, numfg, history = optimize(fg, x, optalg; retract=retract,
                                          precondition=precondition,
                                          (finalize!)=_finalize!,
                                          inner=inner, (transport!)=transport!,
                                          (scale!)=scale!, (add!)=add!,
                                          isometrictransport=true)
    (ΨL, ρR, HL, E, e, hL) = x
    normgrad = sqrt(inner(x, grad, grad))
    if verbosity > 0
        if normgrad <= gradtol
            @info @sprintf("UniformCMPS ground state: converged after %d iterations: e = %.12f, ‖∇e‖ = %.4e",
                           size(history, 1), E, normgrad)
        else
            @warn @sprintf("UniformCMPS ground state: not converged to requested tol: e = %.12f, ‖∇e‖ = %.4e",
                           E, normgrad)
        end
    end
    return ΨL, ρL, ρR, E, e, normgrad, numfg, history
end

function groundstate_unconstrained2(Ĥ::LocalHamiltonian, Ψ₀::UniformCMPS;
                                    gradtol=1e-7,
                                    verbosity=2,
                                    optalg=LBFGS(; gradtol=gradtol,
                                                 verbosity=verbosity - 2),
                                    eigalg=defaulteigalg(Ψ₀),
                                    linalg=defaultlinalg(Ψ₀),
                                    (finalize!)=OptimKit._finalize!,
                                    kwargs...)
    δ = 1
    function retract(x, d, α)
        ΨL, ΨR, C = x
        QL, RLs = ΨL
        QR, RRs = ΨR

        QC = QL * C # == C * QR
        RCs = RLs .* (C,) # == (C,) .* RRs

        dC, dQC, dRCs = d

        C = C + α * dC
        QC = QC + α * dQC
        RCs = RCs .+ α .* dRCs

        RLs = RCs ./ (C,)
        QL = QC / C
        KL = QL + sum(RL' * RL for RL in RLs) / 2
        KL = (KL - KL') / 2
        QL = KL - sum(RL' * RL for RL in RLs) / 2

        ΨL = InfiniteCMPS(QL, RLs; gauge=:l)
        ρR, λ, info_ρR = rightenv(ΨL, C * C'; eigalg=eigalg, linalg=linalg, kwargs...)
        ρR = rmul!(ρR, 1 / tr(ρR[]))
        C = sqrt(ρR)
        QR = C \ QL * C
        RRs = (C,) .\ RLs .* (C,)
        KR = QR + sum(RR * RR' for RR in RRs) / 2
        KR = (KR - KR') / 2
        QR = KR - sum(RR * RR' for RR in RRs) / 2
        ΨR = InfiniteCMPS(QR, RRs; gauge=:r)

        HL, E, e, hL, info_HL = leftenv(Ĥ, (ΨL, one(ρR), C * C'); eigalg=eigalg,
                                        linalg=linalg, kwargs...)
        HR, E, e, hR, info_HR = rightenv(Ĥ, (ΨR, C' * C, one(ρR)); eigalg=eigalg,
                                         linalg=linalg, kwargs...)
        if info_ρR.converged == 0 || info_HL.converged == 0 || info_HR.converged == 0
            @warn "step $α : not converged, e = $E"
            @show info_ρR
            @show info_HL
            @show info_HR
        end
        return (ΨL, ΨR, C, HL, HR, E, e, hL, hR), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        ΨL, ΨR, C, = x
        dC1, dQC1, dRCs1 = d1
        dC2, dQC2, dRCs2 = d2
        RLs = ΨL.Rs
        dWCs1 = dRCs1 .- RLs .* (dC1,)
        dWCs2 = dRCs2 .- RLs .* (dC2,)
        return 2 * real(sum(dot.(dWCs1, dWCs2)))
    end

    function precondition(x, d)
        return d
    end

    function fg(x)
        (ΨL, ΨR, C, HL, HR, E, e, hL, hR) = x
        gradC, gradQC, gradRCs = centergradient(Ĥ, (ΨL, ΨR, C), HL, HR)
        return E, (gradC, gradQC, gradRCs)
    end

    scale!((dC, dQC, dRCs), α) = (rmul!(dC, α), rmul!(dQC, α), rmul!.(dRCs, α))
    function add!((dC1, dQC1, dRCs1), (dC2, dQC2, dRCs2), α)
        return (axpy!(α, dC2, dC1), axpy!(α, dQC2, dQC1), axpy!.(α, dRCs2, dRCs1))
    end

    function _finalize!(x, E, d, numiter)
        normgrad2 = inner(x, d, d)
        δ = max(1e-12, 1e-3 * normgrad2)
        normgrad = sqrt(normgrad2)
        verbosity > 1 &&
            @info @sprintf("UniformCMPS ground state: iter %4d: e = %.12f, ‖∇e‖ = %.4e",
                           numiter, E, normgrad)
        return finalize!(x, E, d, numiter)
    end

    ΨL, = leftgauge(Ψ₀; kwargs...)
    ρR, λ, info_ρR = rightenv(ΨL; eigalg=eigalg, linalg=linalg, kwargs...)
    ρR = rmul!(ρR, 1 / tr(ρR[]))
    C = sqrt(ρR)
    QL, RLs = ΨL
    QR = C \ QL * C
    RRs = (C,) .\ RLs .* (C,)
    KR = QR + sum(RR * RR' for RR in RRs) / 2
    KR = (KR - KR') / 2
    QR = KR - sum(RR * RR' for RR in RRs) / 2
    ΨR = InfiniteCMPS(QR, RRs; gauge=:r)
    HL, E, e, hL, info_HL = leftenv(Ĥ, (ΨL, one(ρR), C * C'); eigalg=eigalg, linalg=linalg,
                                    kwargs...)
    HR, E, e, hR, info_HR = rightenv(Ĥ, (ΨR, C' * C, one(ρR)); eigalg=eigalg,
                                     linalg=linalg, kwargs...)
    x = (ΨL, ΨR, C, HL, HR, E, e, hL, hR)

    if info_ρR.converged == 0 || info_HL.converged == 0 || info_HR.converged == 0
        @warn "initial point not converged, energy = $E"
        @show info_ρR
        @show info_HL
        @show info_HR
    end

    verbosity > 0 &&
        @info @sprintf("UniformCMPS ground state: initialization with e = %.12f", E)

    x, E, grad, numfg, history = optimize(fg, x, optalg; retract=retract,
                                          precondition=precondition,
                                          (finalize!)=_finalize!,
                                          inner=inner, (transport!)=transport!,
                                          (scale!)=scale!, (add!)=add!,
                                          isometrictransport=true)
    (ΨL, ΨR, C, HL, HR, E, e, hL, hR) = x
    normgrad = sqrt(inner(x, grad, grad))
    if verbosity > 0
        if normgrad <= gradtol
            @info @sprintf("UniformCMPS ground state: converged after %d iterations: e = %.12f, ‖∇e‖ = %.4e",
                           size(history, 1), E, normgrad)
        else
            @warn @sprintf("UniformCMPS ground state: not converged to requested tol: e = %.12f, ‖∇e‖ = %.4e",
                           E, normgrad)
        end
    end
    return ΨL, ΨR, C, HL, HR, E, e, normgrad, numfg, history
end

# groundstate with UniformCMPS
function groundstate_constrained(Ĥ::LocalHamiltonian,
                                 Ψ₀::UniformCMPS{<:AbstractMatrix,N},
                                 n₀s::NTuple{N,Number};
                                 gradtol=1e-7,
                                 verbosity=2,
                                 optalg=LBFGS(; gradtol=gradtol, verbosity=verbosity - 2),
                                 eigalg=defaulteigalg(Ψ₀),
                                 linalg=defaultlinalg(Ψ₀),
                                 (finalize!)=OptimKit._finalize!,
                                 chemical_potential_relaxation=1.0,
                                 kwargs...) where {N}
    δ = 1
    μs = ntuple(k -> one(scalartype(Ψ₀)), N)
    n̂s = ntuple(k -> ψ̂[k]' * ψ̂[k], N)
    N̂s = ntuple(k -> ∫(n̂s[k], (-Inf, +Inf)), N)
    Ω̂ = Ĥ - sum(μs .* N̂s)
    function retract(x, d, α)
        ΨL, = x
        QL = ΨL.Q
        RLs = ΨL.Rs
        KL = copy(QL)
        for R in RLs
            mul!(KL, R', R, +1 / 2, 1)
        end

        dRs, dμs = d
        RdR = zero(QL)
        for (R, dR) in zip(RLs, dRs)
            mul!(RdR, R', dR, true, true)
        end

        RLs = RLs .+ α .* dRs
        KL = KL - (α / 2) * (RdR - RdR')
        QL = KL
        for R in RLs
            mul!(QL, R', R, -1 / 2, 1)
        end

        ΨL = InfiniteCMPS(QL, RLs; gauge=:l)
        ρR, λ, info_ρR = rightenv(ΨL, ρR; eigalg=eigalg, linalg=linalg, kwargs...)
        rmul!(ρR, 1 / tr(ρR[]))
        ns = ntuple(k -> expval(n̂s[k], ΨL, ρL, ρR)[], N)
        ΩL, Ω, ω, ωL, info_ΩL = leftenv(Ω̂, (ΨL, ρL, ρR); eigalg=eigalg, linalg=linalg,
                                        kwargs...)

        if info_ρR.converged == 0 || info_ΩL.converged == 0
            @warn "step $α : not converged, ω = $Ω"
            @show info_ρR
            @show info_ΩL
        end

        return (ΨL, ρR, ΩL, Ω, ω, ns, ωL), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        dRs1, dμs1 = d1
        dRs2, dμs2 = d2
        return 2 * real(sum(dot.(dRs1, dRs2))) + sum(dμs1 .* dμs2)
    end

    function precondition(x, d)
        ΨL, ρR, = x
        dRs, dμs = d
        return (dRs .* Ref(posreginv(ρR[0], δ)), zero.(dμs)) # no updates of μ
    end

    function fg(x)
        (ΨL, ρR, ΩL, Ω, ω, ns, ωL) = x

        gradQ, gradRs = gradient(Ω̂, (ΨL, ρL, ρR), ΩL, zero(ΩL); kwargs...)

        Rs = ΨL.Rs

        dRs = .-(Rs) .* Ref(gradQ) .+ gradRs

        dμs = n₀s .- ns

        return Ω, (dRs, dμs)
    end

    scale!(d, α) = (rmul!.(d[1], α), d[2] .* α)
    add!(d1, d2, α) = (axpy!.(α, d2[1], d1[1]), α .* d2[2] .+ d1[2])

    function _finalize!(x, Ω, d, numiter)
        (ΨL, ρR, ΩL, Ω, ω, ns, ωL) = x
        normgrad2 = real(inner(x, d, d))
        normgrad = sqrt(normgrad2)
        E = expval(density(Ĥ), ΨL, ρL, ρR)[]
        dμs = d[2]
        if verbosity > 1
            s = @sprintf("UniformCMPS ground state: iter %4d: ", numiter)
            s *= _groundstate_constraint_infostring(Ω, E, ns, μs, normgrad)
            @info s
        end
        μs = μs .+ chemical_potential_relaxation .* dμs
        Ω̂ = Ĥ - sum(μs .* N̂s)
        δ = max(1e-12, 1e-3 * normgrad2)
        # recompute energy and gradient:
        ΩL, Ω, ω, ωL, info_ΩL = leftenv(Ω̂, (ΨL, ρL, ρR); eigalg=eigalg, linalg=linalg,
                                        kwargs...)

        if info_ρR.converged == 0 || info_ΩL.converged == 0
            @warn "finalizing step with new chemical potential : not converged, ω = $Ω"
            @show info_ρR
            @show info_ΩL
        end

        x = (ΨL, ρR, ΩL, Ω, ω, ns, ωL)
        gradQ, gradRs = gradient(Ω̂, (ΨL, ρL, ρR), ΩL, zero(ΩL); kwargs...)
        Rs = ΨL.Rs
        dRs = .-(Rs) .* Ref(gradQ) .+ gradRs
        d = (dRs, dμs)
        return finalize!(x, Ω, d, numiter)
    end

    ΨL, = leftgauge(Ψ₀; kwargs...)

    ρR, λ, info_ρR = rightenv(ΨL; kwargs...)
    ρL = one(ρR)
    rmul!(ρR, 1 / tr(ρR[]))
    ns = ntuple(k -> expval(n̂s[k], ΨL, ρL, ρR)[], N)
    # rescale initial cMPS to better approximate target densities, using geometric mean
    # this does not change the environments ρL and ρR
    scale_factor = prod(n₀s ./ ns)^(1 / N)
    rmul!(ΨL.Q, scale_factor)
    rmul!.(ΨL.Rs, sqrt(scale_factor))
    ns = ns .* scale_factor
    ΩL, Ω, ω, ωL, info_ΩL = leftenv(Ω̂, (ΨL, ρL, ρR); eigalg=eigalg, linalg=linalg,
                                    kwargs...)

    if info_ρR.converged == 0 || info_ΩL.converged == 0
        @warn "initial point not converged, ω = $Ω"
        @show info_ρR
        @show info_ΩL
    end
    x = (ΨL, ρR, ΩL, Ω, ω, ns, ωL)

    if verbosity > 0
        E = expval(density(Ĥ), ΨL, ρL, ρR)[]
        s = "UniformCMPS ground state: initalization with "
        s *= _groundstate_constraint_infostring(Ω, E, ns)
        @info s
    end

    x, Ω, grad, numfg, history = optimize(fg, x, optalg; retract=retract,
                                          precondition=precondition,
                                          (finalize!)=_finalize!,
                                          inner=inner, (transport!)=transport!,
                                          (scale!)=scale!, (add!)=add!,
                                          isometrictransport=true)
    (ΨL, ρR) = x
    normgrad = sqrt(inner(x, grad, grad))
    e = expval(density(Ĥ), ΨL, ρL, ρR)
    E = e[]
    if verbosity > 0
        if normgrad <= gradtol
            s = @sprintf("UniformCMPS ground state: converged after %d iterations: ",
                         size(history, 1))
        else
            s = "UniformCMPS ground state: not converged to requested tol: "
        end
        s *= _groundstate_constraint_infostring(Ω, E, ns, μs, normgrad)
        @info s
    end
    return ΨL, ρL, ρR, E, e, ns, μs, Ω, normgrad, numfg, history
end

function _groundstate_constraint_infostring(ω, e, ns, μs=nothing, normgrad=nothing)
    s = @sprintf("ω = %.12f, e = %.12f", ω, e)
    N = length(ns)
    if N == 1
        s *= @sprintf(", n = %.6f", ns[1])
        if !isnothing(μs)
            s *= @sprintf(", μ = %.6f", μs[1])
        end
        if !isnothing(μs)
            s *= @sprintf(", ‖∇ω‖ = %.4e", normgrad)
        end
    else
        s *= ", ns = ("
        for k in 1:N
            s *= @sprintf("%.3f", ns[k])
            if k < N
                s *= ", "
            else
                s *= ")"
            end
        end
        if !isnothing(μs)
            s *= ", μs = ("
            for k in 1:N
                s *= @sprintf("%.3f", μs[k])
                if k < N
                    s *= ", "
                else
                    s *= ")"
                end
            end
        end
        if !isnothing(normgrad)
            s *= @sprintf("), ‖∇ω‖ = %.4e", normgrad)
        end
    end
    return s
end

function groundstate(H::LocalHamiltonian, Ψ₀::FourierCMPS;
                     optalg=ConjugateGradient(; verbosity=2, gradtol=1e-7),
                     eigalg=defaulteigalg(Ψ₀),
                     linalg=defaultlinalg(Ψ₀),
                     (finalize!)=OptimKit._finalize!,
                     test=false,
                     kwargs...)
    δ = 1
    function retract(x, d, α)
        ΨL, ρR, HL, = x
        QL = ΨL.Q
        RLs = ΨL.Rs
        dK, dRs = d

        RdR = sum(adjoint.(RLs) .* dRs)
        dRdR = sum(adjoint.(dRs) .* dRs)

        QL = QL + α * dK - α * RdR - (α * α / 2) * dRdR
        RLs = RLs .+ α .* dRs

        ΨL = InfiniteCMPS(QL, RLs; gauge=:l)
        ρR, λ, infoR = rightenv(ΨL, ρR; eigalg=eigalg, linalg=linalg, kwargs...)
        rmul!(ρR, 1 / tr(ρR[0]))
        ρL = one(ρR)
        HL, E, e, hL, infoL = leftenv(H, (ΨL, ρL, ρR); eigalg=eigalg, linalg=linalg,
                                      kwargs...)

        if infoR.converged == 0 || infoL.converged == 0
            @warn "step $α : not converged, energy = $E"
            @show infoR
            @show infoL
        end

        return (ΨL, ρR, HL, E, e, hL), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        dK1, dRs1 = d1
        dK2, dRs2 = d2
        return 2 * real(dot(dK1, dK2)) + 2 * real(sum(dot.(dRs1, dRs2)))
    end

    function fg(x)
        ΨL, ρR, HL, E, e, hL = x

        gradQ, gradRs = gradient(H, (ΨL, one(ρR), ρR), HL, zero(HL); kwargs...)

        Q = ΨL.Q
        RLs = ΨL.Rs

        dK = truncate!((gradQ - gradQ') / 2; Kmax=nummodes(Q))
        dRs = truncate!.((.-(RLs)) .* (gradQ,) .+ gradRs; Kmax=nummodes(RLs[1]))

        return E, (dK, dRs)
    end

    function scale!(d, α)
        dK, dRs = d
        dK = rmul!(dK, α)
        dRs = rmul!.(dRs, α)
        return (dK, dRs)
    end

    function add!(d1, d2, α)
        dK1, dR1s = d1
        dK2, dR2s = d2
        axpy!(α, dK2, dK1)
        axpy!.(α, dR2s, dR1s)
        return (dK1, dR1s)
    end

    # TODO: make this work and test this
    # function precondition(x, d)
    #     ΨL, ρR, = x
    #     dK, dRs = d
    #     ρinv = posreginv(ρR[0], δ)
    #     dKρinv = sylvester(inv(ρinv), inv(ρinv), dK)
    #     return (dKρinv, dRs .* Ref(ρinv))
    # end

    function _finalize!(x, E, d, numiter)
        normgrad2 = real(inner(x, d, d))
        δ = max(1e-12, 1e-1 * normgrad2)
        return finalize!(x, E, d, numiter)
    end

    ΨL = Ψ₀
    ρR, λ, infoR = rightenv(ΨL; kwargs...)
    ρL = one(ρR)
    @assert norm(LeftTransfer(ΨL)(ρL)) < 1e-12
    rmul!(ρR, 1 / tr(ρR[0]))
    HL, E, e, hL, infoL = leftenv(H, (ΨL, ρL, ρR); kwargs...)
    x = (ΨL, ρR, HL, E, e, hL)

    if infoR.converged == 0 || infoL.converged == 0
        @warn "initial point not converged, energy = $E"
        @show infoR
        @show infoL
    end

    if test
        return optimtest(fg, x; alpha=-0.1:0.01:0.1, retract=retract, inner=inner)
    end

    x, E, grad, numfg, history = optimize(fg, x, optalg; retract=retract,
                                          # precondition = precondition, # TODO
                                          (finalize!)=_finalize!,
                                          inner=inner, (transport!)=transport!,
                                          (scale!)=scale!, (add!)=add!,
                                          isometrictransport=true)
    (ΨL, ρR, HL, E, e, hL) = x
    normgrad = sqrt(inner(x, grad, grad))
    return ΨL, one(ρR), ρR, E, e, normgrad, numfg, history
end

function groundstate_diagonal(H::LocalHamiltonian, Ψ₀::UniformCMPS, V, Ss;
                        optalg = ConjugateGradient(; verbosity = 2, gradtol = 1e-7),
                        eigalg = defaulteigalg(Ψ₀),
                        linalg = defaultlinalg(Ψ₀),
                        finalize! = OptimKit._finalize!,
                        kwargs...)

    δ = 1e-3
    function retract(x, d, α)
        ΨL, V, Ss, = x
        QL = ΨL.Q
        RLs = ΨL.Rs
        KL = copy(QL)
        for R in RLs
            mul!(KL, R', R, +1/2, 1)
        end

        dX, dSs = d

        dRs = Ref(dX) .* RLs .+ Ref(V) .* dSs .* Ref(inv(V)) .- RLs .* Ref(dX)
        RdR = zero(KL)
        for (R, dR) in zip(RLs, dRs)
            mul!(RdR, R', dR, true, true)
        end

        V = Constant(exp(α * dX[])) * V
        Ss = Ss .+ α .* dSs

        RLs = Ref(V) .* Ss .* Ref(inv(V))
        KL = KL - (α/2) * (RdR - RdR')
        QL = KL
        for R in RLs
            mul!(QL, R', R, -1/2, 1)
        end
        d = (dX, dSs)

        ΨL = InfiniteCMPS(QL, RLs; gauge = :left)
        ρR, _, infoR = rightenv(ΨL; eigalg = eigalg, linalg = linalg, kwargs...)
        rmul!(ρR, 1/tr(ρR[]))
        ρL = one(ρR)
        HL, E, e, hL, infoL =
            leftenv(H, (ΨL,ρL,ρR); eigalg = eigalg, linalg = linalg, kwargs...)

        if infoR.converged == 0 || infoL.converged == 0
            @warn "step $α : not converged, energy = $e"
            @show infoR
            @show infoL
        end

        return (ΨL, V, Ss, ρR, HL, E, e, hL), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        dV1, dSs1 = d1
        dV2, dSs2 = d2
        s = dV1 === dV2 ? 2*norm(dV1)^2 : 2*real(dot(dV1, dV2))
        for (dSs1,dSs2) in zip(dSs1, dSs2)
            if dSs1 === dSs2
                s += 2*norm(dSs1)^2
            else
                s += 2*real(dot(dSs1, dSs2))
            end
        end
        return s
    end

    function precondition(x, d)
        _, V, Ss, ρR, = x
        copy_S = deepcopy.(Ss)
        Rs = broadcast(x->V*x*inv(V),copy_S)
        dX, dSs = d

        dvec = RecursiveVec(dX,dSs...)

        # turn [dX; dSs] into one large vector and vice versa
        vec_size = length(dX[])+sum(length(diag(s[])) for s in dSs)
        function vectorize(vec)
            cp_dX = deepcopy(dX)
            copyto!(cp_dX[],vec[1:length(cp_dX[])])
            cp_dSs = deepcopy.(dSs)

            offset = length(dX[])
            for s in cp_dSs
                for i in diagind(s[])
                    offset +=1
                    s[][i] = vec[offset]
                end
                
            end

            @assert offset ==  length(vec)

            return (cp_dX,cp_dSs...)
        end
        unvectorize(tup) = reduce(vcat,[tup[1][][:], [diag(t[]) for t in tup[2:end]]...])

        function linear_problem(x)
            dX = x[1]
            _dSs = x[2:end]
            
            dRs = Ref(dX) .* Rs .+ Ref(V) .* _dSs .* Ref(inv(V)) .- Rs .* Ref(dX)
 
            dRs = dRs .* Ref(ρR)
 
            _dSs = Constant.(diagm.((diag.(broadcast(x->x[],(Ref(V') .* dRs .* Ref(inv(V)')))))))
            dX = sum((dRs .* adjoint.(Rs) .- adjoint.(Rs) .* dRs))
            
            bonddim = size(V[],1)
            for i in 1:bonddim
                d = zeros(bonddim)
                d[i] = 1
                s = V[] * diagm(d) * inv(V[])
                dX[] -= dot(s,dX[])/dot(s,s)*s
            end
 
            RecursiveVec(dX,_dSs...)
        end
        
        m = reduce(hcat,map(1:vec_size) do i
            b = zeros(vec_size)
            b[i] = 1
            unvectorize(linear_problem(vectorize(b)))
        end)
          
        dnew = vectorize((δ*one(m) + m)\unvectorize(dvec))
        
        preconditioned_gradient = (dnew[1],dnew[2:end])
        return preconditioned_gradient
    end

    function fg(x)
        (ΨL, V, Ss, ρR, HL, E, e, hL) = x

        gradQ, gradRs = gradient(H, (ΨL, one(ρR), ρR), HL, zero(HL); kwargs...)

        Q = ΨL.Q
        Rs = ΨL.Rs

        dRs = .-(Rs) .* Ref(gradQ) .+ gradRs

        dSs = Constant.(diagm.((diag.(broadcast(x->x[],(Ref(V') .* dRs .* Ref(inv(V)')))))))

        dX = sum((dRs .* adjoint.(Rs) .- adjoint.(Rs) .* dRs))
        
        bonddim = size(V[],1)
        for i in 1:bonddim
            d = zeros(bonddim)
            d[i] = 1
            s = V[] * diagm(d) * inv(V[])
            dX[] -= dot(s,dX[])/dot(s,s)*s
        end

        return E, (dX, dSs)
    end

    function scale!(d, α)
        dV, dSs = d
        rmul!(dV, α)
        for dS in dSs
            rmul!(dS, α)
        end
        return d
    end
    function add!(d1, d2, α)
        dV1, dS1s = d1
        dV2, dS2s = d2
        axpy!(α, dV2, dV1)
        for (dS1, dS2) in zip(dS1s, dS2s)
            axpy!(α, dS2, dS1)
        end
        return d1
    end

    function _finalize!(x, E, d, numiter)
        normgrad2 = real(inner(x, d, d))
        δ = max(1e-12, 1e-2*normgrad2)
        return finalize!(x, E, d, numiter)
    end

    ΨL₀ = Ψ₀
    ρR, _, infoR = rightenv(ΨL₀; kwargs...)
    ρL = one(ρR)
    rmul!(ρR, 1/tr(ρR[]))
    HL, E, e, hL, infoL = leftenv(H, (ΨL₀,ρL,ρR); kwargs...)
    x = (ΨL₀, V, Ss, ρR, HL, E, e, hL)

    x, E, normgrad, numfg, history =
    optimize(fg, x, optalg; retract = retract,
                            finalize! = _finalize!,
                            precondition = precondition,
                            inner = inner, transport! = transport!,
                            scale! = scale!, add! = add!,
                            isometrictransport = true)

    (ΨL, V, Ss, ρR, HL, E, e, hL) = x
    return ΨL, ρR, E, e, normgrad, numfg, history, V, Ss
end

function groundstate_diagonal2(H::LocalHamiltonian, Ψ₀::UniformCMPS, V, Ss;
                        optalg = ConjugateGradient(; verbosity = 2, gradtol = 1e-7),
                        eigalg = defaulteigalg(Ψ₀),
                        linalg = defaultlinalg(Ψ₀),
                        finalize! = OptimKit._finalize!,
                        kwargs...)

    δ = 1e-3
    function retract(x, d, α)
        ΨL, V, Ss, = x
        QL = ΨL.Q
        RLs = ΨL.Rs
        KL = copy(QL)
        for R in RLs
            mul!(KL, R', R, +1/2, 1)
        end

        dX, dSs = d

        dRs = Ref(dX) .* RLs .+ Ref(V) .* dSs .* Ref(inv(V)) .- RLs .* Ref(dX)
        RdR = zero(KL)
        for (R, dR) in zip(RLs, dRs)
            mul!(RdR, R', dR, true, true)
        end

        V = Constant(exp(α * dX[])) * V
        Ss = Ss .+ α .* dSs

        RLs = Ref(V) .* Ss .* Ref(inv(V))
        KL = KL - (α/2) * (RdR - RdR')
        QL = KL
        for R in RLs
            mul!(QL, R', R, -1/2, 1)
        end
        d = (dX, dSs)

        ΨL = InfiniteCMPS(QL, RLs; gauge = :left)
        ρR, _, infoR = rightenv(ΨL; eigalg = eigalg, linalg = linalg, kwargs...)
        rmul!(ρR, 1/tr(ρR[]))
        ρL = one(ρR)
        HL, E, e, hL, infoL =
            leftenv(H, (ΨL,ρL,ρR); eigalg = eigalg, linalg = linalg, kwargs...)

        if infoR.converged == 0 || infoL.converged == 0
            @warn "step $α : not converged, energy = $e"
            @show infoR
            @show infoL
        end

        return (ΨL, V, Ss, ρR, HL, E, e, hL), d
    end

    transport!(v, x, d, α, xnew) = v # simplest possible transport

    function inner(x, d1, d2)
        dV1, dSs1 = d1
        dV2, dSs2 = d2
        s = dV1 === dV2 ? 2*norm(dV1)^2 : 2*real(dot(dV1, dV2))
        for (dSs1,dSs2) in zip(dSs1, dSs2)
            if dSs1 === dSs2
                s += 2*norm(dSs1)^2
            else
                s += 2*real(dot(dSs1, dSs2))
            end
        end
        return s
    end

    function precondition(x, d)
        _, V, Ss, ρR, = x
        copy_S = deepcopy.(Ss)
        Rs = broadcast(x->V*x*inv(V),copy_S)
        dX, dSs = d

        dvec = RecursiveVec(dX,dSs...)

        # turn [dX; dSs] into one large vector and vice versa
        vec_size = length(dX[])+sum(length(diag(s[])) for s in dSs)
        function vectorize(vec)
            cp_dX = deepcopy(dX)
            copyto!(cp_dX[],vec[1:length(cp_dX[])])
            cp_dSs = deepcopy.(dSs)

            offset = length(dX[])
            for s in cp_dSs
                for i in diagind(s[])
                    offset +=1
                    s[][i] = vec[offset]
                end
                
            end

            @assert offset ==  length(vec)

            return (cp_dX,cp_dSs...)
        end
        unvectorize(tup) = reduce(vcat,[tup[1][][:], [diag(t[]) for t in tup[2:end]]...])

        function linear_problem(x)
            dX = x[1]
            _dSs = x[2:end]
            
            dRs = Ref(dX) .* Rs .+ Ref(V) .* _dSs .* Ref(inv(V)) .- Rs .* Ref(dX)
 
            dRs = dRs .* Ref(ρR)
 
            _dSs = Constant.(diagm.((diag.(broadcast(x->x[],(Ref(V') .* dRs .* Ref(inv(V)')))))))
            dX = sum((dRs .* adjoint.(Rs) .- adjoint.(Rs) .* dRs))
            
            bonddim = size(V[],1)
            for i in 1:bonddim
                d = zeros(bonddim)
                d[i] = 1
                s = V[] * diagm(d) * inv(V[])
                dX[] -= dot(s,dX[])/dot(s,s)*s
            end
 
            RecursiveVec(dX,_dSs...)
        end
        
        m = reduce(hcat,map(1:vec_size) do i
            b = zeros(vec_size)
            b[i] = 1
            unvectorize(linear_problem(vectorize(b)))
        end)
          
        dnew = vectorize((δ*one(m) + m)\unvectorize(dvec))
        
        preconditioned_gradient = (dnew[1],dnew[2:end])
        return preconditioned_gradient
    end

    function fg(x)
        (ΨL, V, Ss, ρR, HL, E, e, hL) = x

        gradQ, gradRs = gradient(H, (ΨL, one(ρR), ρR), HL, zero(HL); kwargs...)

        Q = ΨL.Q
        Rs = ΨL.Rs

        dRs = .-(Rs) .* Ref(gradQ) .+ gradRs

        dSs = Constant.(diagm.((diag.(broadcast(x->x[],(Ref(V') .* dRs .* Ref(inv(V)')))))))

        dX = sum((dRs .* adjoint.(Rs) .- adjoint.(Rs) .* dRs))
        
        bonddim = size(V[],1)
        for i in 1:bonddim
            d = zeros(bonddim)
            d[i] = 1
            s = V[] * diagm(d) * inv(V[])
            dX[] -= dot(s,dX[])/dot(s,s)*s
        end

        return E, (dX, dSs)
    end

    function scale!(d, α)
        dV, dSs = d
        rmul!(dV, α)
        for dS in dSs
            rmul!(dS, α)
        end
        return d
    end
    function add!(d1, d2, α)
        dV1, dS1s = d1
        dV2, dS2s = d2
        axpy!(α, dV2, dV1)
        for (dS1, dS2) in zip(dS1s, dS2s)
            axpy!(α, dS2, dS1)
        end
        return d1
    end

    function _finalize!(x, E, d, numiter)
        normgrad2 = real(inner(x, d, d))
        δ = max(1e-12, 1e-2*normgrad2)
        return finalize!(x, E, d, numiter)
    end

    ΨL₀ = Ψ₀
    ρR, _, infoR = rightenv(ΨL₀; kwargs...)
    ρL = one(ρR)
    rmul!(ρR, 1/tr(ρR[]))
    HL, E, e, hL, infoL = leftenv(H, (ΨL₀,ρL,ρR); kwargs...)
    x = (ΨL₀, V, Ss, ρR, HL, E, e, hL)

    x, E, normgrad, numfg, history =
    optimize(fg, x, optalg; retract = retract,
                            finalize! = _finalize!,
                            precondition = precondition,
                            inner = inner, transport! = transport!,
                            scale! = scale!, add! = add!,
                            isometrictransport = true)

    (ΨL, V, Ss, ρR, HL, E, e, hL) = x
    return ΨL, ρR, E, e, normgrad, numfg, history, V, Ss
end